# Raw Lifting

In the previous two documents on the [Formalization Structure](1.%20Formalization%20Structure.md)
and [Reactor Components](2.%20Reactor%20Components.md) we defined raw reactor components and created "proper" counterparts by enforcing "well-formedness". While the "proper" components are fully defined for `Reaction` and `Change`, the `Reactor` type is unfinished in the
sense that it still "leaks" `Raw` types. For example, if we want to access a reactor `σ`'s reactions, the only option we have is `σ.raw.rcns`, which returns `Raw.Reaction`s. 

Our goal was to extend the `Reactor` type in such a way, that it could be used as if it were defined as:

```lean
-- This is not how the Reactor type is actually defined!
structure Reactor (ι υ) [ID ι] [Value υ] where
  ports : Ports ι υ   
  roles : ι ▸ Ports.Role
  state : StateVars ι υ
  nest : ι ▸ Reactor ι υ
  rcns : ι ▸ Reaction ι υ
  prios : PartialOrder ι
  constraints : ....
```

Thus, what we're missing are precisely the projections that would be generated for such a structure.
Some of these projections can be defined trivially:

```lean
-- Components/Reactor/Projections.lean

def Reactor.ports (rtr : Reactor ι υ) : Ports ι υ       := rtr.raw.ports
def Reactor.roles (rtr : Reactor ι υ) : ι ▸ Ports.Role  := rtr.raw.roles
def Reactor.state (rtr : Reactor ι υ) : StateVars ι υ   := rtr.raw.state
def Reactor.prios (rtr : Reactor ι υ) : PartialOrder ι  := rtr.raw.prios
```

We can't define such trivial projections for `nest` and `rcns` as they would expose `Raw` components.
Instead, we need to define these projections is such a way that they "lift" the raw reactors and reactions
to their "proper" counterparts. 

Thus, we define `nest` and `rcns` in two steps:

1. We create functions (all called `fromRaw`) that allow us to lift a raw component to its "proper"
   counterpart.
2. We define `nest` and `rcns` based on those `fromRaw` functions.

## Lifting Functions

We define lifting functions for `(Raw).Reactor`, `(Raw).Reaction` and `(Raw).Change`.

### `Reactor`

Lifting a `Raw.Reactor` is trivial. In fact, we've already defined it by the definition of `Reactor`'s constructor:

```lean
structure Reactor (ι υ) [ID ι] [Value υ] where
  fromRaw ::
    raw : Raw.Reactor ι υ
    rawWF : raw.wellFormed  
```

A `Raw.Reactor` can be lifted to a `Reactor` by using `Reactor.fromRaw` and providing a proof of its well-formedness.
Note therefore that lifting a raw reactor only works under the assumption that we can prove certain properties about it - i.e. not every raw reactor can be lifted.
This becomes more obvious when considering `Reaction.fromRaw`.

### `Reaction`

Lifting a raw reaction requires us to prove `tsSubInDeps`, `outDepOnly` and `normNoChild` (the constraints that are part of `Reaction`):

```lean
-- Components/Lift.lean

def Reaction.fromRaw {rtr : Raw.Reactor ι υ} (hw : rtr.wellFormed) {raw : Raw.Reaction ι υ} (hr : ∃ i, rtr.rcns i = raw) : Reaction ι υ := {
  deps := raw.deps,
  triggers := raw.triggers,
  children := raw.children,
  body := (λ p s => (raw.body p s).attach.map (λ c => Change.fromRaw hw hr c.property)),
  tsSubInDeps := (hw.direct.rcnsWF hr).tsSubInDeps,
  outDepOnly := by
    ...
    have hw := (hw.direct.rcnsWF hr).outDepOnly p s v ho
    ...
  normNoChild := by
    ...
    have hn := (hw.direct.rcnsWF hr).normNoChild
    ...
}
```

We can prove these properties by using the constraints enforced by `Raw.Reactor.wellFormed.direct.rcnsWF`.
To be able to *access* these constraints, we require that the `raw` reaction we intend to lift is contained
in the reactions (`hr`) of a raw reactor `rtr` which is well-formed (`hw`).

Note that while the raw reaction's `deps`, `triggers` and `children` can be trivially lifted, the `body`
requires an extra step. This is because a `Raw.Reaction`' body returns `Raw.Change`s, while a "proper"
`Reaction`'s body returns "proper" `Change`s.
We therefore lift all `Raw.Change` returned by `raw`'s body to "proper" `Change`s using `Change`'s lifting function:

### `Change`

Turning a `Raw.Change` into a `Change` is trivial for all constructors except the `create` constructor, where we
need to turn a `Raw.Reactor` into a `Reactor`. This is only possible if the raw reactor is well-formed, which is 
why the `Change.fromRaw` function requires a host of preconditions which allow us to prove this fact:

```lean
def Change.fromRaw
  {rtr : Raw.Reactor ι υ} (hw : rtr.wellFormed) 
  {rcn : Raw.Reaction ι υ} (hr : ∃ i, rtr.rcns i = rcn) 
  {raw : Raw.Change ι υ} {p s} (hc : raw ∈ Raw.Reaction.body rcn p s) : 
  Change ι υ :=
  match hm:raw with 
    | Raw.Change.port target value  => Change.port target value  
    | Raw.Change.state target value => Change.state target value 
    | Raw.Change.connect src dst    => Change.connect src dst    
    | Raw.Change.disconnect src dst => Change.disconnect src dst 
    | Raw.Change.delete rtrID       => Change.delete rtrID
    | Raw.Change.create cr id => 
      let cr' := Reactor.fromRaw _ (by
          rw [hm] at hc
          have ha := Raw.Reactor.isAncestorOf.creatable hr.choose_spec hc
          exact Raw.Reactor.isAncestorOf_preserves_wf ha hw
      )
      Change.create cr' id
```

## Non-Trivial Projections

Using these lifting functions, we can define "proper" `nest` and `rcns` projections for `Reactor`:

```lean
-- Components/Reactor/Projections.lean

def Reactor.nest (rtr : Reactor ι υ) : ι ▸ Reactor ι υ := 
  let raw : Finmap ι (Raw.Reactor ι υ) := { lookup := rtr.raw.nest, finite := rtr.rawWF.direct.nestFiniteRtrs }
  raw.map' (λ _ h => Reactor.fromRaw _ (by
      have ⟨_, hm⟩ := Finmap.values_def.mp h
      have h' := Raw.Reactor.isAncestorOf.nested hm
      exact Raw.Reactor.isAncestorOf_preserves_wf h' rtr.rawWF
    )
  )

def Reactor.rcns (rtr : Reactor ι υ) : ι ▸ Reaction ι υ :=
  let raw : Finmap ι (Raw.Reaction ι υ) := { lookup := rtr.raw.rcns, finite := rtr.rawWF.direct.rcnsFinite }
  raw.map' $ λ _ h => Reaction.fromRaw rtr.rawWF (Finmap.values_def.mp h)
```

These projections actually do two things at once:

1. The `let`-expressions on the first lines respectively turn the partial functions `rtr.raw.nest` and `rtr.raw.rcns`
into actual `Finmap`s by using the finiteness constraints from `rtr.rawWF`. These finmaps still map to raw components
though.

2. The `λ`-expressions after the `raw.map'` is where we do the actual lifting using the `fromRaw` functions.

## Raw Equivalence

While these projections might *feel* correct, who's to say that we haven't (e.g.) made a mistake in how we lift `Reaction`s?

To ensure that we perform lifting properly, we will prove that lifted components are in fact "equivalent" to the raw
component from which they were lifted. The precise definition of equivalence is different for each component, but it
generally comes down to showing that the "proper" and the raw instances contain the same data. Since our definitions of
"proper" components are generally the same as the raw components, just with additional proofs added on top, this comparison
of "same data" is very direct.

The relation that expresses equivalence between a `Reaction` and a `Raw.Reaction` looks as follows:

```lean
-- Components/Lift.lean

structure Reaction.rawEquiv (rcn : Reaction ι υ) (raw : Raw.Reaction ι υ) : Prop :=
  deps :     rcn.deps = raw.deps
  triggers : rcn.triggers = raw.triggers
  children : rcn.children = raw.children
  body :     ∀ p s, List.forall₂ Change.rawEquiv (rcn.body p s) (raw.body p s)
```

The `rawEquiv` relation only holds when both reactions are built from the "same" data.
For `deps`, `triggers` and `children` this can be expressed as a direct equality.
Since the `body`s of a `Reaction` and a `Raw.Reaction` don't produce the same type of data
(`Change` vs. `Raw.Change`), we can't compare them using equality. Instead, we say that they
must produce only raw-equivalent changes, which is defined as:

```lean
inductive Change.rawEquiv (c : Change ι υ) (raw : Raw.Change ι υ) : Prop
  | port       {t v} :    (c = Change.port t v)       → (raw = Raw.Change.port t v)                         → rawEquiv c raw
  | state      {t v} :    (c = Change.state t v)      → (raw = Raw.Change.state t v)                        → rawEquiv c raw
  | connect    {s d} :    (c = Change.connect s d)    → (raw = Raw.Change.connect s d)                      → rawEquiv c raw
  | disconnect {s d} :    (c = Change.disconnect s d) → (raw = Raw.Change.disconnect s d)                   → rawEquiv c raw
  | create     {r r' i} : (c = Change.create r i)     → (raw = Raw.Change.create r' i)    → (r.rawEquiv r') → rawEquiv c raw
  | delete     {i}   :    (c = Change.delete i)       → (raw = Raw.Change.delete i)                         → rawEquiv c raw
```

The `rawEquiv` relation for changes only holds when both changes are built from "the same" constructor with the same data.
The `create` constructor is special again in that the "proper" constructor takes a `Reactor`, while the raw constructor takes a
`Raw.Reactor`. In this case we also need to require that those reactors are raw-equivalent. Luckily, raw-equivalence for
reactors is trivial:

```lean
def Reactor.rawEquiv (rtr : Reactor ι υ) (raw : Raw.Reactor ι υ) : Prop :=
  rtr.raw = raw
```

### Raw Equivalence for Lifting Functions & Projections

Using these `rawEquiv` relations, we can show that our lifting functions are well-behaved.
The following theorems prove that the `fromRaw` functions only produce raw-equivalent components.

```lean
-- Components/Lift.lean

theorem Reactor.fromRaw_rawEquiv {rtr : Reactor ι υ} {raw h} : 
  rtr = Reactor.fromRaw (raw := raw) h → rtr.rawEquiv raw :=
  ...

theorem Change.fromRaw_rawEquiv {c : Change ι υ} {rtr rcn raw p s hw hr hc} :
  c = @Change.fromRaw _ _ _ _ rtr hw rcn hr raw p s hc → c.rawEquiv raw :=
  ...

theorem Reaction.fromRaw_rawEquiv {rcn : Reaction ι υ} {rtr raw hw hr} :
  rcn = @Reaction.fromRaw _ _ _ _ rtr hw raw hr → rcn.rawEquiv raw := 
  ...
```

Using these theorems we prove that the `nest` and `rcns` projections behave as intended:

```lean
-- Components/Reactor/Projections.lean

theorem Reactor.nest_rawEquiv (rtr : Reactor ι υ) : Finmap.forall₂' Reactor.rawEquiv rtr.nest rtr.raw.nest := ...

theorem Reactor.rcns_rawEquiv (rtr : Reactor ι υ) : Finmap.forall₂' Reaction.rawEquiv rtr.rcns rtr.raw.rcns := ...
```

Note that we don't define an explicit notion of raw-equivalence for types `ι ▸ Reaction ι υ` and `ι ▸ Reactor ι υ`
respectively. Instead, we use `Finmap.forall₂'` which relates a finmap and a partial function such that the relation
is only fulfilled if they are defined for the same inputs (`eqIDs` below) and all outputs of the finmap and function are related by a given relation (`rel` below):

```lean
structure forall₂' (r : β → γ → Prop) (f₁ : α ▸ β) (f₂ : α → Option γ) : Prop where
  eqIDs : ∀ a, a ∈ f₁.ids ↔ f₂ a ≠ none
  rel : ∀ {a} {b : β} {c : γ}, (f₁ a = b) → (f₂ a = c) → r b c
```

## Constraints

Thus, we can now access a `Reactor`'s data without ever interacting with `Raw` components.
The only part that is still only accessible in raw form are a reactor's constraints.
While *some* of these constraints were only added so that we could lift raw components to proper
components (e.g. `rcnsWF`), not *all* constraints are of this kind.
In the previous document on [Reactor Components](2.%20Reactor%20Components.md) we classified the
constraints contained in `Raw.Reaction.wellFormed` into three categories:

1. Reaction Constraints
2. Reactor Constraints
3. ID Constraints

The first category was used for lifting, but will never need to be accessed beyond that (as those constraints
live on in the definition of `Reaction`).
Categories (2) and (3) *do* belong to a proper reactor's definition though and thus should be exposed.
We therefore need to lift these constraints such that they make statements about proper components instead of 
raw components (as they currently do).

We lift these constraints by proving theorems that make analogous statements about proper components.

E.g. `Raw.Reaction.wellFormed` contains:

```lean
mutsLinearOrder : ∀ i₁ i₂ m₁ m₂, rtr.rcns i₁ = some m₁ → rtr.rcns i₂ = some m₂ → m₁.isMut → m₂.isMut → (rtr.prios.le i₁ i₂ ∨ rtr.prios.le i₂ i₁) 
```

We lift this constraint be proving:

```lean
-- Components/Reactor/Properties.lean

theorem Reactor.mutsLinearOrder {rtr : Reactor ι υ} {i₁ i₂ : ι} (h₁ : i₁ ∈ rtr.muts.ids) (h₂ : i₂ ∈ rtr.muts.ids) : 
  rtr.prios.le i₁ i₂ ∨ rtr.prios.le i₂ i₁ := 
  ...
```

The statements of both constraints are basically equivalent. The lifted version just restates the raw version
in terms of proper components. This is reflected in the fact that proving such lifted statements mainly consists 
of finding a way to apply the raw version.

The other constraints of category (2) are lifted as follows:

```lean
theorem Reactor.wfRoles (rtr : Reactor ι υ) : rtr.roles.ids = rtr.ports.ids := 
  ...

theorem Reactor.wfNormDeps {rtr : Reactor ι υ} {n : Reaction ι υ} (r : Ports.Role) (h : n ∈ rtr.norms.values) : 
  n.deps r ⊆ (rtr.ports' r).ids ∪ rtr.nestedPortIDs r.opposite :=
  ...

theorem Reactor.wfMutDeps {rtr : Reactor ι υ} {m : Reaction ι υ} (r : Ports.Role) (h : m ∈ rtr.muts.values) : 
  (m.deps Role.in ⊆ (rtr.ports' Role.in).ids) ∧ (m.deps Role.out ⊆ (rtr.ports' Role.out).ids ∪ rtr.nestedPortIDs Role.in) :=
  ...

theorem Reactor.mutsBeforeNorms {rtr : Reactor ι υ} {iₙ iₘ : ι} (hn : iₙ ∈ rtr.norms.ids) (hm : iₘ ∈ rtr.muts.ids) : 
  rtr.prios.lt iₘ iₙ :=
  ...
```

Lifting the ID constraint `Raw.Reaction.wellFormed.uniqueIDs` requires an extra step, as the notion of a `Lineage`
needs to be lifted as well:

```lean
inductive Reactor.Lineage : Reactor ι υ → ι → Type _ 
  | rtr {σ i} : i ∈ σ.nest.ids  → Lineage σ i
  | rcn {σ i} : i ∈ σ.rcns.ids  → Lineage σ i
  | prt {σ i} : i ∈ σ.ports.ids → Lineage σ i
  | stv {σ i} : i ∈ σ.state.ids → Lineage σ i
  | nest {σ : Reactor ι υ} σ' {i} i' : (Lineage σ' i) → (σ.nest i' = some σ') → Lineage σ i

theorem uniqueIDs {σ : Reactor ι υ} {i} (l₁ l₂ : Lineage σ i) : l₁ = l₂ :=
  ...
```

---

This concludes the process of sealing "proper"-land from `Raw`-land.
We can now use `Reactor` as if it were defined as:

```lean
structure Reactor (ι υ) [ID ι] [Value υ] where
  ports : Ports ι υ   
  roles : ι ▸ Ports.Role
  state : StateVars ι υ
  nest : ι ▸ Reactor ι υ
  rcns : ι ▸ Reaction ι υ
  prios : PartialOrder ι
  wfRoles : ....
  wfNormDeps : ...
  wfMutDeps : ...
  mutsBeforeNorms : ...
  mutsLinearOrder : ...
```