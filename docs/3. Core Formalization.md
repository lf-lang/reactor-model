# Core Formalization

***This document is a WIP.***

---

The recursive nature of reactors (as a result of nested reactors) makes it non-trivial to define some basic components of the Reactor model: reactors, mutations, and nested reactors (here called *networks*).
As a result, the formalizations of these objects may be hard to understand at first glance.
The aim of this document is to provide an overview of *how* and *why* we ended up with the given formalization.

## Preliminaries

The recursive nature of reactors is visualized by the following figure.

![Reactor Visualization](images/reactor.png)

Since reactors can contain nested reactor networks, their definition is fundamentally self-referential.
In and of itself this is not an issue, as *inductive types* are a suitable tool for self-referential definitions (cf. Lean's `Nat` type).
Problems only arise once we try to deviate from the "standard" scheme for inductive types, as dictated by the Calculus of Inductive Constructions (CiC).

### Nested Inductives

The type theoretic underpinnings of Lean impose certain constraints on the ways in which inductive types can be declared (via inductive schemes).
The constraint that is of interest to us, is the following:
If a constructor of an inductive type `I` takes a parameter `p` whose type also refers to `I` (thereby making it recursive), then the type of `p` has to have the form `Î±â‚ â†’ Î±â‚‚ â†’ ... â†’ I`.
That is, recursive appearances of `I` are only allowed in the form that `I` is the final output of some function (or `p` may simply have type `I`).

Since this is quite a strong constraint to uphold when defining objects, Lean has some affordances for working around this.
That is, Lean allows us to define constructors that do *not* respect the aforementioned constraint, as long as it is able to derive an equivalent definition that *does* respect the constraint.
The most common case of this seems to be parameters whose types have the form `Wrapper I`, where `Wrapper` is some other inductive type (that takes a parameter) like `List`.
In the context of inductive type constructors, these kinds of types are called *nested inductives*.

The reason why this is relevant to the formalization of reactors is twofold:

1. A formalization of reactors naturally leads to the use of nested inductives (as we will see later).
2. Not every nested inductive can be resolved by Lean. 
   That is, we will encounter certain `Wrapper` types for which Lean will not allow `Wrapper I` in an inductive type constructor.

### Mutually Inductive Types

The figure (of a reactor) above shows that reactors are recursive.
What it does not show is exactly which *subcomponents* of a reactor are part of the recursive definition.
For example, reactions can be defined fully independently of reactors and do not require a recursive definition.
Mutations on the other hand can return reactors as output, and therefore depend on the definition of reactors.
The entire picture of type interdependencies looks as follows.

![Type Dependencies](images/type-dependencies.png)

That is, the definition of `Reactor` depends on the definition of `Mutation`, which depends on the definition of `MutationOutput`, etc.
Since these dependencies form a cycle, the definitions are all recursive.
But since none of the types depends *directly* on itself, this kind of recursion is called *mutual recursion*.

Mutual recursion between types can be formalized in Lean via *mutually inductive types*.
Mutually inductive types are not present in CiC, but just as Lean is able to compile nested inductives down to "valid" types, it is able to take mutually inductive types and "flatten" them for us.

The reason why this is relevant to the formalization of reactors is again twofold:

1. A formalization of reactors naturally leads to the use of mutually inductive types.
2. Lean doesn't support `structure`s as part of mutually inductive definitions. 
   That is, even though all of the relevant types will only have *one* constructor, we will need to define them as regular `inductive` types. 
   As a result, we will have to manually define projections on them.

## Primitives 

Before we dive into the formalization of reactors, we briefly need to cover some of the primitives upon which they'll be built:

* Identifiers
* Values
* Ports
* State variables

*(All of these are defined in `Primitives.lean`.)*

Identifiers and values are supposed to be "opaque" types. 
As a result we never actually define them, but rather make all other types dependent over two type parameters `Î¹` (for identifiers) and `Ï…` (for values).
As we do have *some* requirements for these types, we constrain them to the `ID` and `Value` type classes respectively.
These type classes both require `DecidableEq`, and define an `ID.root` (notation: `âŠ¤`) and `Value.absent` (notation: `âŠ¥`) - the details are subject to change though.

Reactors' `Ports` and `StateVars` are nothing more than `Finmap`s (notation: `â–¸`) from the identifier to the value type: `Î¹ â–¸ Ï…`.
`Finmap`s can be thought of as hash tables/dictionaries/associative arrays, or (more mathematically) as partial functions defined for only finitely many inputs:

```lean
structure Finmap (Î± Î²) where
  lookup : Î± â†’ Option Î² 
  finite : (Set.univ.image lookup).finite
```

A quirk of the `Finmap` type used here is that it is not the finite map type defined by mathlib.
We define our own `Finmap`, because mathlib defines finite maps using a *quotient type*. 
An unfortunate aspect of quotient types is that they can't be used as nested inductives.
We will require `Finmap` for nested inductives in the definition of reactors and hence need to avoid mathlib's quotient-based definition of finite maps.

For `Ports` we define some additional niceties:

* We often need to be able to differentiate between input and output ports. 
  To facilitate this we use `Ports.Role`, which is an enumeration type with an `in` and an `out` case.

* More importantly, we define a special kind of accessor for ports, written as `p[i]`.
  This accessor tries to get the value associated with ID `i` in ports `p`, but only returns the value if it is not the absent value.
  That is, while accessing a value in `p` naturally might fail (if the given ID has no defined value in `p`) and hence has to return an optional value, the `[...]` accessor also returns `none` if it finds the absent value.
  We define this accessor, because often we only care about whether there *is* a value for a given ID or not.
  And in many contexts the absent value is supposed to have the same meaning as the actual absence of a value (even though `âŠ¥` technically is a member of `Ï…`).

## Reactors' Formalization

We'll start by giving a high-level overview of the steps involved in the formalization of reactors (without explanation).

1. Define `Raw.MutationOutput`, `Raw.Mutation`, `Raw.Reactor` and `Raw.Network` as mutually inductive types, without any of the constraints required for "proper" reactors.
2. Add projections for the properties of these types (as exist for types declared with `structure`).
3. Define `Reactor` as a `Raw.Reactor` satisfying the constraints required for "proper" reactors.
4. Define `MutationOutput`, `Mutation` and `Network` as structures, with all necessary constraints to be "proper", by referencing the definition of `Reactor`.
5. Add accessors on `Reactor` that return instances of `Mutation` and `Network` instead of their `Raw` variants, by converting the underlying `Raw.Reactor`'s instances into "proper" ones by using the constraints that are part of `Reactor`.

### 1. Raw Definitions

The mutually inductive definitions of reactors and their components are:

```lean
namespace Raw

mutual 

inductive MutationOutput (Î¹ Ï…) [i : ID Î¹] [v : Value Ï…]
  | mk
    (prtVals : Ports Î¹ Ï…)
    (state   : StateVars Î¹ Ï…)
    (newCns  : List (Î¹ Ã— Î¹))
    (delCns  : List (Î¹ Ã— Î¹))
    (newRtrs : List (Reactor Î¹ Ï…))
    (delRtrs : Finset Î¹)

inductive Mutation (Î¹ Ï…) [i : ID Î¹] [v : Value Ï…]
  | mk 
    (deps : Ports.Role â†’ Finset Î¹) 
    (triggers : Finset Î¹)
    (body : Ports Î¹ Ï… â†’ StateVars Î¹ Ï… â†’ MutationOutput Î¹ Ï…)

inductive Reactor (Î¹ Ï…) [i : ID Î¹] [v : Value Ï…]
  | mk 
    (ports : Ports.Role â†’ Ports Î¹ Ï…) 
    (state : StateVars Î¹ Ï…)
    (rcns : Î¹ â–¸ Reaction Î¹ Ï…)
    (muts : Î¹ â–¸ Mutation Î¹ Ï…)
    (prios : PartialOrder Î¹)
    (nest : Network Î¹ Ï…)
  
inductive Network (Î¹ Ï…) [i : ID Î¹] [v : Value Ï…]
  | mk 
    (rtrs : Î¹ â–¸ (Reactor Î¹ Ï…))
    (cns : Finset (Connection Î¹))

end

end Raw
```

While the details aren't important, there are some notable aspects:

* The definitions live in the `Raw` namespace.
  This is to distinguish them from the "proper" definitions, which we'll cover later.
* The type class instances required by all types are named (`i` and `v`). 
  This is necessary as Lean requires all type-level parameters of mutually inductive definitions to have the same name.
  (So the `Î¹` and `Ï…` parameters also need to have the same name across all definitions.)
* `Raw.Reactor.rncs`, `Raw.Reactor.muts` and `Network.nodes` use `Finmap` in a nested inductive. 
  This is why we created the custom (non-quotient) definition of `Finmap`.
* `Raw.Reactor.rncs` references the type `Reaction`, which isn't part of this `mutual` block.
  This is because `Reaction` can be defined fully independently of reactors and hence is already defined in `Components/Reactions.lean`.
* The types defined above are simply buckets for data, i.e. there are no `Prop`-based constraints.
  There are two reasons for this.
  On the one hand, this is another case of the limits of nested inductives.
  It seems that Lean can't handle most propositions as nested inductives - hence we have to omit them here.
  The other reason is that to even state propositions over these types, we'd need to use their recursors, which aren't accessible within the definitions yet.
  For more information on this point cf. a [related Zulip thread](https://leanprover.zulipchat.com/#narrow/stream/270676-lean4/topic/.E2.9C.94.20Use.20recursor.20within.20inductive.20def).
  Of course we still need these reactor-components to satisfy certain constraints, so we add them in a different way in Step 3.

As you can see, these `Raw` definitions leave a lot to be desired. 
In the following steps we'll wrap these types in such a way that we'll never have to use these `Raw` variants directly.

### 2. Structure-esque Projections

First, we need to add a convenience for the following steps.
All of the `Raw` types are fundamentally structure types, but had to be defined as plain `inductive` types.
Hence, we add the following kind of projections to all of them:

```lean
namespace Raw.Mutation

def deps :     Mutation Î¹ Ï… â†’ (Ports.Role â†’ Finset Î¹)                          | mk d _ _ => d
def triggers : Mutation Î¹ Ï… â†’ (Finset Î¹)                                       | mk _ t _ => t
def body :     Mutation Î¹ Ï… â†’ (Ports Î¹ Ï… â†’ StateVars Î¹ Ï… â†’ MutationOutput Î¹ Ï…) | mk _ _ b => b

end Raw.Mutation
```

### 3. "Proper" `Reactor`s

The first step towards creating "proper" (non-`Raw`) components is making sure that the required constraints are enforced.

We *could* create a subtype for each `Raw` type that adds the required restrictions for *that* type, but then any nested components will still be the `Raw` variant.
For example, if we added the necessary constraints to `Raw.Mutation`:

```lean
-- This is a negative example - not how `Mutation` is going to be defined!
structure Mutation where
  raw : Raw.Mutation
  tsSubInDeps : raw.triggers âŠ† raw.deps Role.in
  inDepOnly : âˆ€ {i i'} s, (i =[raw.deps Role.in] i') â†’ raw.body i s = raw.body i' s
  outDepOnly : âˆ€ i s {o}, (o âˆ‰ raw.deps Role.out) â†’ (raw.body i s).prtVals[o] = none 
```

... then the mutation's `body` (here `.raw.body`) would still return a `Raw.MutationOutput`, which uses `Raw.Reactor`, which uses `Raw.Mutation`.
Hence, we would only wrap the `Raw` components "one layer deep".

The next logical step would therefore be to add more constraints on `Mutation`, which ensure that *all nested components* are "proper", no matter how deep we drill.
Hence, we'd add the constraints for *all* components on just *one* initial component.
This is in fact precisely what we do in our formalization - but we choose `Reactor` as that initial component:

```lean
structure Reactor where
  raw : Raw.Reactor Î¹ Ï…
  wf : raw.wellFormed  
```

Here `Raw.Reactor.wellFormed` is the proposition that contains the constraints for all components nested inside `Reactor.raw`.

> Defining this proposition is an entire topic of its own, as it requires recursion that Lean can't determine to be terminating by itself.
> [ANDRÃ‰S' PART]?

Hence, even though the API for `Reactor` is still terrible (we're still working with `Raw.Mutation`, `Raw.Network`, etc.), we now have a concept of a "proper" constrained `Reactor`.
Now we just need to create proper variants of all other components.

### 4. Spreading Properness

Turning the other components into "proper" variants is actually quite simple, since we have `Reactor` to build upon.
In fact, we can really just define `MutationOutput`, `Mutation` and `Network` the way we would have, if they weren't recursive.
For example, `MutationOutput` is just a simple structure:

```lean
structure MutationOutput (Î¹ Ï…) [ID Î¹] [Value Ï…] where
  prtVals : Ports Î¹ Ï…
  state   : StateVars Î¹ Ï…
  newCns  : List (Î¹ Ã— Î¹)
  delCns  : List (Î¹ Ã— Î¹)
  newRtrs : List (Reactor Î¹ Ï…)
  delRtrs : Finset Î¹
```

The `Reactor` in `newRtrs` is the "proper" reactor type that we just defined in Step 3.
The definition of `Mutation` can now simply build on `MutationOutput`:

```lean
structure Mutation (Î¹ Ï…) [ID Î¹] [Value Ï…] where
  deps : Ports.Role â†’ Finset Î¹ 
  triggers : Finset Î¹
  body : Ports Î¹ Ï… â†’ StateVars Î¹ Ï… â†’ MutationOutput Î¹ Ï…
  tsSubInDeps : triggers âŠ† deps Role.in
  inDepOnly : âˆ€ {i i'} s, (i =[deps Role.in] i') â†’ body i s = body i' s
  outDepOnly : âˆ€ i s {o}, (o âˆ‰ deps Role.out) â†’ (body i s).prtVals[o] = none 
```

> The definition of `Network` can be formalized analogously.

So now we have "proper" versions of all components, but there's still a problem:
The definition of `Reactor` doesn't use `Mutation` or `Network` in its own definition.
In fact, it *can't*, or else we'd be right back in mutually-inductive territory.
So `Reactor` still returns `Raw` variants of everything.
Fixing this issue will be the final step in separating `Raw`-land from "proper"-land.

### 5. Sealing `Reactor`

Separating `Raw`-land from "proper"-land basically amounts to an API-trick - the definition of `Reactor` will stay exactly the same.

Currently, if we want to access any properties of a `Reactor`, we need to go through its `raw` property.
A natural first step to fixing this is to add projections manually, kind of like we did in Step 2:

```lean
namespace Reactor

def ports (rtr : Reactor Î¹ Ï…) : Ports.Role â†’ Ports Î¹ Ï… := rtr.raw.ports
def state (rtr : Reactor Î¹ Ï…) : StateVars Î¹ Ï…          := rtr.raw.state
def rcns  (rtr : Reactor Î¹ Ï…) : Î¹ â–¸ Reaction Î¹ Ï…       := rtr.raw.rcns
def prios (rtr : Reactor Î¹ Ï…) : PartialOrder Î¹         := rtr.raw.prios

end Reactor
```

Now there's two projections missing, those for `muts` and `nest`.
If we were to add a naive projection for `muts`, it would look similar:

```lean
def muts (rtr : Reactor Î¹ Ï…) : Î¹ â–¸ Raw.Mutation Î¹ Ï… := rtr.raw.muts
```

The problem is that here we get into `Raw` territory again (`Raw.Mutation`) - so this is the exact point where we need to seal "proper"-land.
We do this by returning "proper" `Mutation`s instead of `Raw.Mutation`s.
The reason why we can do this is that we have `Reaction.wf` and can use those constraints to turn `Raw.Mutation`s into "proper" `Mutation`s:

```lean
-- Defined in `Components/Mutation.lean`.

def muts (rtr : Reactor Î¹ Ï…) : Î¹ â–¸ Mutation Î¹ Ï… := 
  rtr.raw.muts.map ... -- Uses `rtr.wf`.
```

We can then do the same for the `nest` property, transforming `Raw.Network` into `Network` by using the constraints contained in `Reactor.wf`.

Thus, `Reactor` has accessors that completely allow us to avoid `Raw` components, and all other definitions of "proper" components don't even have a way of accessing `Raw` components.
We have successfully sealed "proper"-land (hopefully ðŸ‘€).